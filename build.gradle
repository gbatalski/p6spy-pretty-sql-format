['java', 'signing', 'maven', 'idea'].each {
    apply plugin: it
}

group = 'de.batalski'
version = '1.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile "p6spy:p6spy:2.0.0"
    compile "org.hibernate:hibernate-core:4.3.11.Final"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0-M3"
}

repositories {
    mavenCentral()
}

// javadoc.jar generation
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
// sources.jar generation
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// summarize artifacts
artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

/* see here for more details http://yennicktrevels.com/blog/2013/10/11/automated-gradle-project-deployment-to-sonatype-oss-repository/ */
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: releasesRepositoryUrl) {
                authentication(userName: repositoryUsername, password: repositoryPassword)
            }

            pom.project {
                resolveStrategy = DELEGATE_FIRST
                name "${project.group}:${project.name}"
                description 'Pretty SQL formatter from Hibernate integrated with P6Spy'
                url 'https://github.com/gbatalski/p6spy-pretty-sql-format'

                scm {
                    url 'http://github.com/gbatalski/p6spy-pretty-sql-format/tree/master'
                    connection 'scm:git:git://github.com/gbatalski/p6spy-pretty-sql-format/.git'
                    developerConnection 'scm:git:ssh://github.com:gbatalski/p6spy-pretty-sql-format.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gbatalski'
                        name 'Ghenadii Batalski'
                        email 'ghenadii.batalski AT gmail.com'
                    }
                }
            }
        }
    }
}